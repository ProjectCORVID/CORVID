#!/usr/bin/env coffee

process = require 'process'
coffee = require 'coffee-script'

[execName, scriptName, args...] = process.argv

if args.length < 2
  console.log 'Must provide a referent and method name'
  process.exit 0

app = require '../server/server'

[refName, methName, params...] = args

app.models.Referent.find name: refName
  .then (found) ->
    [referent, rest...] = found
    app.models.Behavior.find referentId: referent.id
  .then ([behavior]) ->
    methods = behavior.package.methods
    if method = methods[methName]
      if not method.fn
        method.fn = coffee.eval method.code
      Promise.resolve method.fn.call behavior, params...
    else
      Promise.reject 'method not found'
  .then (results) ->
    console.log "Success (#{results})"
    process.exit 0
  .catch (err) ->
    console.log "There was an error: ", err
    process.exit 1
